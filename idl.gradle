// Copyright 2016 Proyectos y Sistemas de Mantenimiento SL (eProsima).
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

// In this section you declare the used plugins
// community plugins
plugins {
    id "me.champeau.gradle.antlr4" version "0.1" // antlr4 community plugin.
}

// internal plugins
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'eclipse' // Eclipse integration

description = """"""

ext {
    publishURL = project.hasProperty('publishURL') ? project.publishURL.toString() : ''
    publishUsername = project.hasProperty('publishUsername') ? project.publishUsername.toString() : ''
    publishPassword = project.hasProperty('publishPassword') ? project.publishPassword.toString() : ''
}

repositories {
    mavenCentral()
}

dependencies {
    antlr4 'org.antlr:antlr4:4.5'
    compile group: 'org.antlr', name: 'stringtemplate', version:'3.2'
    testCompile group: 'junit', name: 'junit', version:'4.10'
}

antlr4.source = project.file("src/main/antlr4/omg")
antlr4.output = project.file("src/main/generated-java")
antlr4.listener = false
antlr4.visitor = false
antlr4.extraArgs=['-package', 'com.eprosima.idl.parser.grammar']

// make the Java compile task depend on the antlr4 task
compileJava.dependsOn antlr4
compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.compilerArgs.add('-Xlint:deprecation')
    options.compilerArgs.add('-Xlint:unchecked')
}

// add the generated source files to the list of java sources

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir 'src/it/java' //integration tests
            srcDir antlr4.output
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
    test {
        java {
            //unitary test here
        }
    }
}

// add antlr4 to classpath
configurations {
   compile.extendsFrom antlr4
}

group = "com.eprosima"
version = "1.0.0"

//general properties
jar {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    baseName = 'idlparser'
    manifest {
        attributes("Created-By": "eProsima", "Main-Class": "com.eprosima.idl.test.TestIDLParser")
    }
}

task sourceJar(type: Jar, dependsOn: classes) {
   classifier = 'sources'
   from sourceSets.main.allJava
}

/**
 * Set up publishing. User must set the following properties
 * in ~/.gradle/gradle.properties or pass via -Pproperty=value
 * on command line.
 *
 * publishUsername=yourusername
 * publishPassword=yourpassword
 * publishUrl=https://some.url/maven/orgname/reponame/projectname
 */
publishing {
   publications {
      mavenJava(MavenPublication) {
         artifactId = "idl-parser"
         from components.java

         artifact sourceJar {
            classifier 'sources'
         }
      }
   }
   repositories {
      maven {
         url publishURL
         credentials.username = publishUsername
         credentials.password = publishPassword
      }
   }
}